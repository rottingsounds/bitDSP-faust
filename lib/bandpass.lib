svf2blti(cf, q, k, in) =   tick
                           ~ ( _ ,
                               _) : (  ! ,
                                       ! ,
                                       _ ,
                                       _ ,
                                       _ ,
                                       _ ,
                                       _ ,
                                       _ ,
                                       _ ,
                                       _ ,
                                       _ ,
                                       _)
      with {
           div(x1, x2) = x1 / ba.if(    x2 < 0, 
                                        min(ma.EPSILON * -1, x2), 
                                        max(ma.EPSILON, x2));  
           w(f) = 2 * ma.PI * f / ma.SR;
           r = div(1, (2 * q));
           wa = (2 * ma.SR) * tan(w(cf) / 2);
           g = wa / ma.SR / 2;
           tick(s1, s2) =  u1 ,
                           u2 ,
                           lp ,
                           hp ,
                           bp ,
                           bp_norm ,
                           ls ,
                           hs ,
                           b_shelf ,
                           notch ,
                           peak ,
                           ap
               with {
                   u1 = v1 + bp;
                   u2 = v2 + lp;
                   v1 = hp * g;
                   v2 = bp * g;
                   hp = div((in - 2 * r * s1 - g * s1 - s2),
                               (1 + 2 * r * g + g * g));
                   bp = s1 + v1;
                   lp = s2 + v2;
                   bp_norm = bp * 2 * r;
                   b_shelf = in + k * bp_norm;
                   ls = in + k * lp;
                   hs = in + k * hp;
                   notch = in - bp_norm;
                   ap = in - 4 * r * bp;
                   peak = lp - hp;
               };
      };
bp(cf, q, x) = svf2blti(cf, q, 1, x) : ba.selectn(10,3);

